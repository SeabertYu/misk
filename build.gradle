plugins {
  id "org.jetbrains.kotlin.jvm" version "1.3.11" apply false
  id "com.vanniktech.maven.publish" version "0.4.0" apply false
  id 'com.gradle.build-scan' version '2.1'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

ext {
  isCi = "true".equals(System.getenv('CI'))
  rep = null
}
apply from: file("./dependencies.gradle")

if (rootProject.file("repositories.gradle").exists()) {
  apply from: rootProject.file("repositories.gradle")
}

subprojects {
  apply plugin: "java"
  apply plugin: 'kotlin'
  buildscript {
    repositories {
      mavenCentral()
      jcenter()
      if (rep != null) {
        if (rep.maven) {
          for (Map.Entry<String, String> repEntry : rep.maven) {
            maven {
              url repEntry.value
            }
          }
        }
      }
    }

    dependencies {
      classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
  }
  repositories {
    mavenCentral()
    jcenter()
    if (rep != null) {
      if (rep.maven) {
        for (Map.Entry<String, String> repEntry : rep.maven) {
          maven {
            url repEntry.value
          }
        }
      }
    }
  }
  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      allWarningsAsErrors = true
    }
  }
  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      allWarningsAsErrors = true
    }
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
  }

  dependencies {
    testImplementation dep.junitApi
    testRuntimeOnly dep.junitEngine
  }
  test {
    useJUnitPlatform()
    testLogging {
      events "started", "passed", "skipped", "failed"
      exceptionFormat = 'full'
    }
  }
  if (rootProject.file("hooks.gradle").exists()) {
    apply from: rootProject.file("hooks.gradle")
  }
}

